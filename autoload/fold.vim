" vim: set foldmethod=expr :
" ËæÖÂä©Êï∞ÊçÆ -------------------------------------------------------------- {{{1
let s:titlePrefix = {
            \ 'r': '#',
            \ 'stata': '*',
            \ 'vim': '"',
            \ }
let s:endInfoFunName = {
            \ 'r': 's:R_GetFoldEndInfo',
            \ 'stata': 's:Stata_GetFoldEndInfo',
            \ 'vim': 's:Vim_GetFoldEndInfo',
            \ }

" ËæÖÂä©ÂáΩÊï∞ ============================================================== {{{1
function! s:CheckEndCondition(content, indent, condition_list)
    for cond in a:condition_list
        if a:content =~? cond.regex && a:indent <= cond.indent
            return (cond.contain_end_line ? 1 : 0)
        endif
    endfor
    return -2
endfunction

function! s:IndentLevel(lnum)
    return indent(a:lnum) / &shiftwidth
endfunction

function! s:GetEmbededFoldLevels(lnum, p_level = 0, num = 10) abort
    let endinfo      = call(s:endInfoFunName[&l:filetype], [a:lnum])
    if empty(endinfo) | return [] | endif

    let end_condition = endinfo.end_condition
    let current_level = a:p_level + 1
    let levels        = [ current_level ]
    let current       = a:lnum + 1
    let last          = line('$')

    while current <= last
        let c_content      = getline(current)
        let c_indent_level = <sid>IndentLevel(current)

        let c_end = <sid>CheckEndCondition(c_content, c_indent_level, end_condition)
        if c_end != -2  
            if c_end == 1 | call add(levels, current_level) | endif
            let current_level -= 1
            break
        endif

        let c_manual_level = s:GetManualFoldLevel(current, s:titlePrefix[&l:filetype])
        if c_manual_level > 0
            call add(levels, ">" . (a:p_level + c_manual_level + 1))
            let current_level = a:p_level + c_manual_level + 1
            let current += 1
            continue
        endif

        let embed_fold_levels =
            \ s:GetEmbededFoldLevels(current, current_level, a:num)
        if len(embed_fold_levels) > 0
            call extend(levels, embed_fold_levels)
            let current += len(embed_fold_levels)
            continue
        endif

        call add(levels, current_level)
        let current += 1
    endwhile

    if len(levels) < a:num
        call map(levels, {_, val -> val - 1})
    else
        let levels[0] = ">" . levels[0]
    endif

    return levels
endfunction

function! s:GetMarkerFoldLevel(lnum)
    let content         = getline(a:lnum)
    let marker          = split(&l:foldmarker, ",")[0]
    let search_regex    = '\V' . marker . '\zs\d\*'
    let marker_position = match(content, search_regex)
    if marker_position == -1
        return -2
    endif  

    let marker_on_comment = match(
        \ v:lua.extract_hl_group_link(0, a:lnum - 1, marker_position - 1),
        \ '\vComment|Special'
        \ )
    if marker_on_comment == -1 
        return -2
    endif
    
    let marker_level = matchstr(content, search_regex)
    if strlen(marker_level) == ""
        let marker_level = 0
    endif

    return marker_level
endfunction

function! s:GetManualFoldLevel(lnum, title_prefix)
    let marker_level = <sid>GetMarkerFoldLevel(a:lnum)
    if marker_level > 0
        return marker_level
    endif

    let title_level = <sid>GetTitleLevel(a:lnum, a:title_prefix)
    if title_level > 0
        return title_level
    endif

    return -2
endfunction

function! s:GetTitleLevel(lnum, prefix = '*')
    let content = getline(a:lnum)
    let prefix_length = strlen(a:prefix)
    let regex_title = '\V\^\s\*\zs'
            \ . '\('.a:prefix.'\)\+' . '\s\+'
            \ . '\(\d\+.\)\+\d\*' . '\ze\s\+'

    let prefix_number = matchstr(content, regex_title)
    if prefix_number == ""
        return -2
    endif

    let prefix_symbol = matchstr(prefix_number, '\V\(' . a:prefix . '\)\+')
    let prefix_num    = strlen(prefix_symbol) / prefix_length

    let title_number  = matchstr(prefix_number, '\v(\d+\.)+\d*')
    let title_level   = len(split(title_number, '\.'))

    return (title_level + prefix_num - 1)
endfunction

function! s:R_GetFoldEndInfo(lnum)
    let content          = getline(a:lnum)
    let indent_level     = <sid>IndentLevel(a:lnum)
    let end_condition    = []

    if <sid>GetMarkerFoldLevel(a:lnum) == 0
        let syntax_name = "manual"
        let end_condition = [
                    \{ 'regex': '\v^\s*$', 
                    \  'indent': indent_level,
                    \  'contain_end_line': v:false },
                    \]
    elseif content =~? '\v<switch\('
        let syntax_name = "switch"
        let end_condition = [
                    \ { 'regex': '\V\^\s\*\[)\]}]',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ },
                    \ { 'regex': '\v\S',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:false,
                    \ },
                    \]
    elseif content =~? '\v^\s*[A-z$:.]+[([]"?'
        let syntax_name = 'function_call'
        let end_condition = [
                    \ { 'regex': '\V\^\[\s"\]\*\[)\]}]',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ },
                    \ { 'regex': '\v\S',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:false,
                    \ }
                    \]
    elseif content =~? '\v\<+\-'
        let syntax_name = 'assign'
        let end_condition = [
                    \ { 'regex': '\V\^\s\*\[)\]}]',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ },
                    \ { 'regex': '\v\S',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:false,
                    \ }
                    \]
    elseif content =~? '\v\s*<(if \(|for \(|else).*\s+\{'
        let syntax_name = 'condtion'
        let end_condition = [
                    \ { 'regex': '\V\^\s\*}(\s+\#.*)?$',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ },
                    \ { 'regex': '\v\S',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:false,
                    \ },
                    \ ]
    elseif content =~? '\v(\%[A-z<]?\>\%|\|\>|\+)(\s+\#.*)?$'
        let syntax_name = 'pipe'
        let end_condition = [
                    \ { 'regex': '\v\S',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:false,
                    \ },
                    \]
    else
        return []
    endif
    return { 'syntax_name': syntax_name, 'end_condition': end_condition}
endfunction

function! s:Stata_GetFoldEndInfo(lnum)
    let content          = getline(a:lnum)
    let indent_level     = <sid>IndentLevel(a:lnum)
    let end_condition    = []
    if <sid>GetMarkerFoldLevel(a:lnum) == 0
        let syntax_name = "manual"
        let end_condition = [
                    \{ 'regex': '\v^\s*$', 
                    \  'indent': indent_level,
                    \  'contain_end_line': v:false },
                    \]
    elseif content =~? '\v^\s*preserve>'
        let syntax_name = 'preserve'
        let end_condition = [
                    \ { 'regex': '\v^\s*restore>',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ }
                    \ ]
    elseif content =~? '\v^\s*snappreserve>'
        let syntax_name = "snap"
        let end_condition = [
                    \ { 'regex': '\v^\s*snaprestore>',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ }
                    \ ]
    elseif content =~? '\v^\s*program>'
        let syntax_name = "program"
        let end_condition = [
                    \ { 'regex': '\v^\s*end',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ }
                    \ ]
    elseif content =~? '\v\s*(if|else|quietly|noisi(ly)?|foreach|forvalue)>.*\{(\s+\/\/.*)?$'
        let syntax_name = "flow"
        let end_condition = [
                    \ { 'regex': '\v^\s*\}',
                    \   'indent': indent_level,
                    \   'contain_end_line': v:true,
                    \ }
                    \ ]
    else
        return {}
    endif
    return { 'syntax_name': syntax_name, 'end_condition': end_condition}
endfunction

function! s:Vim_GetFoldEndInfo(lnum) abort
    let content          = getline(a:lnum)
    let indent_level     = <sid>IndentLevel(a:lnum)
    let end    = {
               \   'regex': "",
               \   'indent': indent_level,
               \   'contain_end_line': v:true,
               \ }

    if <sid>GetMarkerFoldLevel(a:lnum) == 0
        let syntax_name           =  "manual"
        let end.regex             =  '\v^\s*$'
        let end.contain_end_line  =  v:false
    elseif content =~? '\v^\s*function'
        let syntax_name           =  "function"
        let end.regex             =  '\v^\s*endfunc'
    elseif content =~? '\v^\s*if>' && content !~? '\v\|\s+endif(\s+\".*)?$'
        let syntax_name           =  "if"
        let end.regex             =  '\v^\s*endif>'
    elseif content =~? '\v^\s*for>' && content !~? '\v\|\s+endfor(\s+\".*)?$'
        let syntax_name           =  "for"
        let end.regex             =  '\v^\s*endfor>'
    elseif content =~? '\v^\s*while>' && content !~? '\v\|\s+endwhile(\s+\".*)?$'
        let syntax_name           =  "while"
        let end.regex             =  '\v^\s*endwhile>'
    elseif content =~? '\v^\s*augroup\s(END)@!'
        let syntax_name           = 'augroup'
        let end.regex             = '\v^\s*augroup\s(END)@='
    elseif content =~? '\v\s*\=\s*\{'
        let syntax_name = 'assign_dict'
        let end.regex = '\v^\s*[^\\ ]'
        let end.contain_end_line = v:false
    elseif content =~? '\v\s*\=\s*\['
        let syntax_name = 'assign_list'
        let end.regex = '\v^\s*[^\\ ]'
        let end.contain_end_line = v:false
    else
        return {}
    endif

    return { 'syntax_name': syntax_name, 'end_condition': [end]}
endfunction

" ÊäòÂè†Ë°®ËææÂºè ============================================================ {{{1
function! fold#GetAllLineFoldLevels(num = 10) abort
    let current = 1
    let lastline = line('$')
    let current_level = 0
    let levels = []

    while current <= lastline
        let manual_level = <sid>GetManualFoldLevel(current, s:titlePrefix[&l:filetype])
        if manual_level > 0
            call add(levels, ">" . manual_level)
            let current_level = manual_level
            let current += 1
            continue
        endif

        let embed_fold_levels = s:GetEmbededFoldLevels(current, current_level, a:num)
        if len(embed_fold_levels) > 0
            call extend(levels, embed_fold_levels)
            let current += len(embed_fold_levels)
            continue
        endif

        call add(levels, current_level)
        let current += 1
    endwhile

    return levels
endfunction

function! fold#GetFold()
    if v:lnum == 1
        let b:lbs_foldlevels = fold#GetAllLineFoldLevels(5)
    endif
    let fdl = b:lbs_foldlevels[v:lnum - 1]        

    if getline(v:lnum) =~? '\v^\s*$'
        let fdl = b:lbs_foldlevels[v:lnum - 2]        
    endif

    if v:lnum == line('$') | unlet b:lbs_foldlevels | endif
    return fdl
endfunction

" Custom fold text adapted from:
"     https://github.com/jdhao/nvim-config/blob/master/autoload/utils.vim
function! fold#FoldText() abort 
    let l:line_width = &l:textwidth == 0 ? 78 : &l:textwidth
    let l:line = getline(v:foldstart)
    let l:fold_line_num = v:foldend - v:foldstart
    let l:marker = split(&l:foldmarker, ",")[0]
    let l:fold_text = substitute(l:line, '\V' . l:marker . '\[0-9]\*', '', 'g')
    let l:fold_text = substitute(l:fold_text, '\v[=\-.* ]*$', '', 'g')
    let foldlevel_num = split("Û∞â≥ Û∞â´ Û∞â¨ Û∞â≠")
    let foldlevel_icon = ["ÔÉâ ", "ÔÉâÔÉâ ", "ÔÉâÔÉâÔÉâ "]
    "let foldlevel_icon = [".", "üåï", "üåí", "..."]
    " let foldlevel_icon = split("üåë üåí üåì üåî üåï üåñ üåó üåò ")
    " let foldlevel_symbol = split("üåÄ‚öΩ ‚öæ ü•é üèÄ üèê üèà üèâ")
    if v:foldlevel >= len(foldlevel_icon)
        let l:foldicon = "ÔΩ¢" . v:foldlevel . "ÔΩ£" 
    else
        let l:foldicon = foldlevel_icon[v:foldlevel - 1]
    endif
    if v:foldlevel >= len(foldlevel_num)
        let l:foldnum = foldlevel_num[0]
    else
        let l:foldnum = foldlevel_num[v:foldlevel]
    endif
    let l:foldicon = repeat("  ", 0)
                \  . l:foldicon
                \  . repeat(" ", indent(l:line) - strdisplaywidth(l:foldicon))
    let l:fill_char = "‚îÄ"
    let l:fold_text = substitute(l:fold_text, '\v^\s{' . strdisplaywidth(l:foldicon) . '}', "", "")
    let l:fill_char_num = l:line_width - strdisplaywidth(l:fold_text) - strdisplaywidth(l:foldicon)
    return printf('%s%s %s %03dL', l:foldicon, l:fold_text,
                \ repeat(l:fill_char, l:fill_char_num - 6),
                \ l:fold_line_num
                \ )
endfunction

